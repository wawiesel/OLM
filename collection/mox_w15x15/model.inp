=t-depl parm=(addnux={{static.addnux}})
PWR-MOX Westinghouse 15x15, 2D quarter assembly model 
{{static.xslib}}
' ----------------------------------------------------------------
'  template to generate libraries for ORIGEN
'  parameters are: wtpt_inner      - wt% plutonium: inner
'                  wtpt_inedge     - wt% plutonium: inside edge
'                  wtpt_edge       - wt% plutonium: edge
'                  wtpt_corner     - wt% plutonium: corner
'                  wtpt_pu238      - wt% Pu238
'                  wtpt_pu239      - wt% Pu239
'                  wtpt_pu240      - wt% Pu240
'                  wtpt_pu241      - wt% Pu241
'                  wtpt_pu242      - wt% Pu242
'                  density_Am      - americium density (g/cc)
'                  fuel_density    - fuel density (g/cc)
'                  coolant_density - coolant density (g/cc)
'                  ppm_boron       - coolant boron content (ppm)
'                  specpow         - average specific power
'                  daystoburn      - depletion interval in days
' ----------------------------------------------------------------
' Documentation and Notes (empty fields are auto-populated):
'  [Change Log] 
'    Rev 0: Generated by J. Doe | 
'    Rev 1: Generated by B. R. Betzler, June 2014 | 
'    Rev 2: Generated by B. R. Betzler, September 2015
'    Rev 3: Updated and generated by B. D. Hiscox, August 2021
'  [Author(s)] B. R. Betzler
'  [SCALE Version] SCALE 6.3
'  [Reactor Type] Mixed Oxide Pressurized Water Reactor Westinghouse 15x15
'  [Model Info] 2D t-depl quarter assembly model (see Figure \ref{fi:mox_w15x15}), {{static.xslib}} cross-section library
'  [Sources] 
'    1. ``Characteristics of Spent Fuel, High-Level Waste, and other Radioactive Wastes which May Require Long-Term Isolation'', Appendix 2A. Physical Descriptions of LWR Fuel Assemblies, DOE/RW-0184, Volume 3 of 6, U.S. DOE Office of Civilian Radioactive Waste Management, 1987. | 
'    2. S. M. Bowman, I. C. Gauld, and J. C. Wagner, ``Recommendations on Fuel Parameters for Standard Technical Specifications for Spent Fuel Storage Casks'', NUREG/CR-6716 (ORNL/TM-2000/385), U.S. Nuclear Regulatory Commission, Oak Ridge National Laboratory, March 2001. | 
'    3. H. Smith, J. Peterson, and J. Hu, ``Fuel Assembly Modeling for the Modeling and Simulation Toolset'', ORNL/LTR-2012-555 Rev. 1, Oak Ridge National Laboratory, 2013. | 
'    4. I. C. Gauld, ``MOX Cross-Section Libraries for ORIGEN-ARP'', ORNL/TM-2003/2, Oak Ridge National Laboratory, Oak Ridge, Tennessee, 2003. |
'    5. U. Mertyurek and I. C. Gauld, ``Development of ORIGEN Libraries for Mixed Oxide (MOX) Fuel Assembly Designs'', to be published, 2015.
'  [Data Range]
'  [Libraries]
'  [Power]
'  [Other Info] 
'    Fuel density, gap gas pressure from Appendix 2A of Reference 1.
'    Temperatures, moderator density from Table 6 of Reference 2.
'    All other dimensions, materials, etc. from Reference 3. 
'    MOX isotopic vector information from Reference 4.
'    MOX zoning pattern from section 4.1 of Reference 5 (see Table 2, Eq.~3, and Eq.~4).
'  figure{mox_w15x15.pdf: MOX PWR Westinghouse 15x15 fuel assembly.}
' ----------------------------------------------------------------
read alias
  $fuel 1 11 21 31 end
  $clad 2 12 22 32 end
  $mod  3 13 23 33 end
  $gap  4 14 24 34 end
end alias
'
' ----------------------------------------------------------------
read comp
'
' fuel inner
  uo2  1 den={{comp.inner.density}} {{comp.inner.uo2.dens_frac}} 923 92234 0.001 92235 0.24 92238 99.759 end
  puo2 1 den={{comp.inner.density}} {{comp.inner.puo2.dens_frac}} 923 94238 {{comp.inner.puo2.iso.pu238}}
                                 94239 {{comp.inner.puo2.iso.pu239}}
                                 94240 {{comp.inner.puo2.iso.pu240}}
                                 94241 {{comp.inner.puo2.iso.pu241}}
                                 94242 {{comp.inner.puo2.iso.pu242}} end

  am   1 den={{comp.inner.density}}
        {{comp.inner.amo2.dens_frac*comp.inner.info.amo2_hm_frac}} 923
        95241 {{comp.inner.amo2.iso.am241}} end
  o    1 den={{comp.inner.density}}
        {{comp.inner.amo2.dens_frac*(1.0-comp.inner.info.amo2_hm_frac)}} 923 end
'
' fuel inside edge
  uo2  11 den={{comp.iedge.density}} {{comp.iedge.uo2.dens_frac}} 923 92234 0.001 92235 0.24 92238 99.759 end
  puo2 11 den={{comp.iedge.density}} {{comp.iedge.puo2.dens_frac}} 923 94238 {{comp.iedge.puo2.iso.pu238}}
                                   94239 {{comp.iedge.puo2.iso.pu239}}
                                   94240 {{comp.iedge.puo2.iso.pu240}}
                                   94241 {{comp.iedge.puo2.iso.pu241}}
                                   94242 {{comp.iedge.puo2.iso.pu242}} end

  am   11 den={{comp.iedge.density}}
        {{comp.iedge.amo2.dens_frac*comp.iedge.info.amo2_hm_frac}} 923
        95241 {{comp.iedge.amo2.iso.am241}} end
  o    11 den={{comp.iedge.density}}
        {{comp.iedge.amo2.dens_frac*(1.0-comp.iedge.info.amo2_hm_frac)}} 923 end
'
' fuel edge
  uo2  21 den={{comp.iedge.density}} {{comp.iedge.uo2.dens_frac}} 923 92234 0.001 92235 0.24 92238 99.759 end
  puo2 21 den={{comp.iedge.density}} {{comp.iedge.puo2.dens_frac}} 923 94238 {{comp.iedge.puo2.iso.pu238}}
                                  94239 {{comp.iedge.puo2.iso.pu239}}
                                  94240 {{comp.iedge.puo2.iso.pu240}}
                                  94241 {{comp.iedge.puo2.iso.pu241}}
                                  94242 {{comp.iedge.puo2.iso.pu242}} end

  am   21 den={{comp.iedge.density}}
        {{comp.iedge.amo2.dens_frac*comp.iedge.info.amo2_hm_frac}} 923
        95241 {{comp.iedge.amo2.iso.am241}} end
  o    21 den={{comp.iedge.density}}
        {{comp.iedge.amo2.dens_frac*(1.0-comp.iedge.info.amo2_hm_frac)}} 923 end
'
' fuel corner
  uo2  31 den={{comp.corner.density}} {{comp.corner.uo2.dens_frac}} 923 92234 0.001 92235 0.24 92238 99.759 end
  puo2 31 den={{comp.corner.density}} {{comp.corner.puo2.dens_frac}} 923 94238 {{comp.corner.puo2.iso.pu238}}
                                  94239 {{comp.corner.puo2.iso.pu239}}
                                  94240 {{comp.corner.puo2.iso.pu240}}
                                  94241 {{comp.corner.puo2.iso.pu241}}
                                  94242 {{comp.corner.puo2.iso.pu242}} end

  am   31 den={{comp.corner.density}}
        {{comp.corner.amo2.dens_frac*comp.corner.info.amo2_hm_frac}} 923
        95241 {{comp.corner.amo2.iso.am241}} end
  o    31 den={{comp.corner.density}}
        {{comp.corner.amo2.dens_frac*(1.0-comp.corner.info.amo2_hm_frac)}} 923 end
'
' clad
  zirc4 $clad 1 595 end 
'
' coolant - water with {{state.ppm_boron}} ppm soluble boron 
  h2o   $mod den={{state.coolant_density}} 1.000000 579 end
  boron $mod den={{state.coolant_density}} {{state.ppm_boron/1.0E6}} 579 end
'
' gap - 300-450psig (set at minimum)
  he-4  $gap den=0.001672 1 595 end
'
' guide tube
  zirc4 5 1 579 end 
'
end comp
'
' ----------------------------------------------------------------
read celldata
  latticecell squarepitch fueld=0.92939 $fuel
                          gapd= 0.94844 $gap
                          cladd=1.07188 $clad
                          pitch=1.43002 $mod  end
  centrmdata alump=0 pmc_dilute=1e30 pmc_omit=0 end centrmdata 
end celldata
'
' ----------------------------------------------------------------
read depletion 
  $fuel
end depletion
'
' ----------------------------------------------------------------
read burndata
  {%- for pb in time.burndata %}
  power={{pb.power}} burn={{pb.burn}} down=0 end
  {%- endfor %}
end burndata
'
' ----------------------------------------------------------------
read model
  PWR-MOX Westinghouse 15x15  
'
read parm
  cmfd=1
  drawit=yes
  echo=yes
  epsinner=-1e-4 
  fillmix=3
  run=yes
  xycmfd=4
  sn=8
  cell_tol=1e-8
end parm
'
read materials
  mix=1  pn=1 com='fuel - inner zone'       end
  mix=2  pn=1 com='clad'                    end
  mix=3  pn=2 com='coolant'                 end
  mix=4  pn=0 com='gap'                     end
  mix=5  pn=1 com='guide tube'              end
  mix=11 pn=1 com='fuel - inside edge zone' end
  mix=21 pn=1 com='fuel - edge zone'        end
  mix=31 pn=1 com='fuel - corner zone'      end
end materials
'
read geom
unit 1 
com='fuel rod - inner zone'
  cylinder 10    0.464695
  cylinder 20    0.47422 
  cylinder 30    0.53594
  cuboid   40  4p0.71501
  media 1 1 10
  media 4 1 20 -10
  media 2 1 30 -20
  media 3 1 40 -30
  boundary  40
unit 2 
com='fuel rod - inside edge zone'
  cylinder 10    0.464695
  cylinder 20    0.47422 
  cylinder 30    0.53594
  cuboid   40  4p0.71501
  media 11 1 10
  media  4 1 20 -10
  media  2 1 30 -20
  media  3 1 40 -30
  boundary  40
unit 3 
com='fuel rod - edge zone'
  cylinder 10    0.464695
  cylinder 20    0.47422 
  cylinder 30    0.53594
  cuboid   40  4p0.71501
  media 21 1 10
  media  4 1 20 -10
  media  2 1 30 -20
  media  3 1 40 -30
  boundary  40
unit 4 
com='fuel rod - corner zone'
  cylinder 10    0.464695
  cylinder 20    0.47422 
  cylinder 30    0.53594
  cuboid   40  4p0.71501
  media 31 1 10
  media  4 1 20 -10
  media  2 1 30 -20
  media  3 1 40 -30
  boundary  40
unit 5
com='guide tube'
  cylinder 10    0.65024
  cylinder 20    0.69342
  cuboid   40  4p0.71501
  media 3 1 10
  media 5 1 20 -10
  media 3 1 40 -20
  boundary  40
unit 11
com='1/2 fuel rod - right - inner zone'
  cylinder 10 0.464695 chord +x=0
  cylinder 20 0.47422  chord +x=0
  cylinder 30 0.53594  chord +x=0
  cuboid   40 0.71501  0.0 2p0.71501
  media 1 1 10
  media 4 1 20 -10
  media 2 1 30 -20
  media 3 1 40 -30
  boundary  40
unit 21
com='1/2 fuel rod - top - inner zone'
  cylinder 10   0.464695 chord +y=0
  cylinder 20   0.47422  chord +y=0
  cylinder 30   0.53594  chord +y=0
  cuboid   40 2p0.71501  0.71501 0.0
  media 1 1 10
  media 4 1 20 -10
  media 2 1 30 -20
  media 3 1 40 -30
  boundary  40
unit 12
com='1/2 fuel rod - right - inside edge zone'
  cylinder 10 0.464695 chord +x=0
  cylinder 20 0.47422  chord +x=0
  cylinder 30 0.53594  chord +x=0
  cuboid   40 0.71501  0.0 2p0.71501
  media 11 1 10
  media  4 1 20 -10
  media  2 1 30 -20
  media  3 1 40 -30
  boundary  40
unit 22
com='1/2 fuel rod - top - inside edge zone'
  cylinder 10   0.464695 chord +y=0
  cylinder 20   0.47422  chord +y=0
  cylinder 30   0.53594  chord +y=0
  cuboid   40 2p0.71501  0.71501 0.0
  media 11 1 10
  media  4 1 20 -10
  media  2 1 30 -20
  media  3 1 40 -30
  boundary  40
unit 13
com='1/2 fuel rod - right - edge zone'
  cylinder 10 0.464695 chord +x=0
  cylinder 20 0.47422  chord +x=0
  cylinder 30 0.53594  chord +x=0
  cuboid   40 0.71501  0.0 2p0.71501
  media 21 1 10
  media  4 1 20 -10
  media  2 1 30 -20
  media  3 1 40 -30
  boundary  40
unit 23
com='1/2 fuel rod - top - edge zone'
  cylinder 10   0.464695 chord +y=0
  cylinder 20   0.47422  chord +y=0
  cylinder 30   0.53594  chord +y=0
  cuboid   40 2p0.71501  0.71501 0.0
  media 21 1 10
  media  4 1 20 -10
  media  2 1 30 -20
  media  3 1 40 -30
  boundary  40
unit 51
com='1/2 guide tube - right'
  cylinder 10 0.65024 chord +x=0
  cylinder 20 0.69342 chord +x=0
  cuboid   40 0.71501 0.0 2p0.71501
  media 3 1 10
  media 5 1 20 -10
  media 3 1 40 -20
  boundary 40
unit 52
com='1/2 guide tube - top'
  cylinder 10   0.65024 chord +y=0
  cylinder 20   0.69342 chord +y=0
  cuboid   40 2p0.71501 0.71501 0.0
  media 3 1 10
  media 5 1 20 -10
  media 3 1 40 -20
  boundary 40
unit 53
com='1/4 instrument tube - NE'
  cylinder 10 0.65024 chord +x=0 chord +y=0
  cylinder 20 0.69342 chord +x=0 chord +y=0
  cuboid   40 0.71501 0.0 0.71501 0.0
  media 3 1 10
  media 5 1 20 -10
  media 3 1 40 -20
  boundary 40
global unit 10
com='1/4 assembly - NE'
  cuboid 10 10.76833 0.0 10.76833 0.0 
  array 1 10 place 1 1 0 0
  media 3 1 10
  boundary 10 48 48
end geom
'
read array
  ara=1 nux=8 nuy=8 typ=cuboidal 
  fill
  53  21  21  21  52  21  22  23
  11  1   1   1   1   1   2   3  
  11  1   1   1   1   5   2   3  
  11  1   1   5   1   1   2   3  
  51  1   1   1   1   1   2   3 
  11  1   5   1   1   5   2   3  
  12  2   2   2   2   2   2   4  
  13  3   3   3   3   3   4   4  end fill
end array
'
read bnds 
  all=mirror
end bnds
'
end model
end
