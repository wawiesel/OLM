=t-depl parm=(addnux={{static.addnux}})
PWR CE 14x14, 2D quarter assembly model 
{{static.xslib}}
' ----------------------------------------------------------------
'  template to generate libraries for ORIGEN-S 
'  parameters are: u235wt%         - wt% U235
'                  u234wt%         - wt% U234
'                  u236wt%         - wt% U236
'                  u238wt%         - wt% U238
'                  coolant_density - coolant density (g/cc)
'                  ppm_boron       - Coolant boron concentration (ppm)           
'                  specpow         - average specific power (MW/MTHM)
'                  daystoburn      - depletion interval in days
' Documentation and Notes (empty fields are auto-populated):
'  [Change Log] 
'    Rev 0: Generated by J. Doe | 
'    Rev 1: Generated by B. R. Betzler, June 2014
'    Rev 2: Updated and generated by B. D. Hiscox, August 2021
'  [Author(s)] B. R. Betzler
'  [SCALE Version] SCALE 6.3
'  [Reactor Type] Pressurized Water Reactor Combustion Engineering 14x14
'  [Model Info] 2D t-depl quarter assembly model (see Figure \ref{fi:ce14x14}), {{static.xslib}} cross-section library
'  [Sources] 
'    1. ``Characteristics of Spent Fuel, High-Level Waste, and other Radioactive Wastes which May Require Long-Term Isolation'', Appendix 2A. Physical Descriptions of LWR Fuel Assemblies, DOE/RW-0184, Volume 3 of 6, U.S. DOE Office of Civilian Radioactive Waste Management, 1987. | 
'    2. S. M. Bowman, I. C. Gauld, and J. C. Wagner, ``Recommendations on Fuel Parameters for Standard Technical Specifications for Spent Fuel Storage Casks'', NUREG/CR-6716 (ORNL/TM-2000/385), U.S. Nuclear Regulatory Commission, Oak Ridge National Laboratory, March 2001. | 
'    3. H. Smith, J. Peterson, and J. Hu, ``Fuel Assembly Modeling for the Modeling and Simulation Toolset'', ORNL/LTR-2012-555 Rev. 1, Oak Ridge National Laboratory, 2013.
'  [Data Range]
'  [Libraries]
'  [Power]
'  [Other Info] 
'    Fuel density, gap gas pressure from Appendix 2A of Reference 1.
'    Temperatures, moderator density from Table 6 of Reference 2.
'    All other dimensions, materials, etc. from Reference 3.
'  figure{ce14x14.pdf: PWR CE 14x14 fuel assembly.}
' ----------------------------------------------------------------
read comp
'
' fuel - 94-95% theoretical density (10.96g/cc) (set to max)
  uo2   1 den={{comp.density}} 1 873 92234 {{comp.uo2.iso.u234}} 
                           92235 {{comp.uo2.iso.u235}} 
                           92236 {{comp.uo2.iso.u236}} 
                           92238 {{comp.uo2.iso.u238}} end 
'
' clad
  zirc4 2 1 620 end 
'
' coolant - water with {{state.ppm_boron}} ppm soluble boron 
  h2o   3 den={{state.coolant_density}} 1.000000 570 end
  boron 3 den={{state.coolant_density}} {{state.ppm_boron/1.0E6}} 570 end
'
' gap - 300-450psig (set at minimum)
  he-4  4 den=0.001605 1 620 end
'
' guide tube
  zirc4 5 1 570 end 
'
end comp
'
' ----------------------------------------------------------------
read celldata
  latticecell squarepitch fueld=0.95631 1
                          gapd= 0.97536 4
                          cladd=1.1176  2
                          pitch=1.4732  3 end
  centrmdata alump=0 pmc_dilute=1e30 pmc_omit=0 end centrmdata
end celldata
'
' ----------------------------------------------------------------
read depletion 
  1 
end depletion
'
' ----------------------------------------------------------------
read burndata
  {%- for pb in time.burndata %}
  power={{pb.power}} burn={{pb.burn}} down=0 end 
  {%- endfor %}
end burndata
'
' ----------------------------------------------------------------
read model
  PWR CE 14x14   
'
read parm
  cmfd=1
  drawit=yes
  echo=yes 
  epsinner=-1e-4 
  fillmix=3
  run=yes
  xycmfd=4
  sn=8
  cell_tol=1e-8
end parm
'
read materials
  mix=1 pn=1 com='fuel'       end
  mix=2 pn=1 com='clad'       end
  mix=3 pn=2 com='coolant'    end
  mix=4 pn=0 com='gap'        end
  mix=5 pn=1 com='guide tube' end
end materials
'
read geom
unit 1
com='fuel rod'
  cylinder 10   0.478155
  cylinder 20   0.48768
  cylinder 30   0.5588
  cuboid   40 4p0.7366
  media 1 1 10
  media 4 1 20 -10
  media 2 1 30 -20 
  media 3 1 40 -30
  boundary 40
unit 51
com='1/4 guide tube - NE'
  cylinder 10 1.31445 chord +x=0 chord +y=0
  cylinder 20 1.41605 chord +x=0 chord +y=0
  cuboid   30 1.4732  0 1.4732 0
  media 3 1 10
  media 5 1 20 -10
  media 3 1 30 -20 
  boundary 30
unit 52
com='1/4 guide tube - SE'
  cylinder 10 1.31445 origin x=0 y=1.4732 chord +x=0 chord -y=1.4732
  cylinder 20 1.41605 origin x=0 y=1.4732 chord +x=0 chord -y=1.4732
  cuboid   30 1.4732  0 1.4732 0
  media 3 1 10
  media 5 1 20 -10
  media 3 1 30 -20 
  boundary 30
unit 53
com='1/4 guide tube - SW'
  cylinder 10 1.31445 origin x=1.4732 y=1.4732 chord -x=1.4732 chord -y=1.4732
  cylinder 20 1.41605 origin x=1.4732 y=1.4732 chord -x=1.4732 chord -y=1.4732
  cuboid   30 1.4732  0 1.4732  0
  media 3 1 10
  media 5 1 20 -10
  media 3 1 30 -20 
  boundary 30
unit 54
com='1/4 guide tube - NW'
  cylinder 10 1.31445 origin x=1.4732 y=0 chord -x=1.4732 chord +y=0
  cylinder 20 1.41605 origin x=1.4732 y=0 chord -x=1.4732 chord +y=0
  cuboid   30 1.4732  0 1.4732 0
  media 3 1 10
  media 5 1 20 -10
  media 3 1 30 -20 
  boundary 30
global unit 10
com='1/4 assembly - NE'
  cuboid 10 10.39 0 10.39 0
  array 1 10 place 1 1 0 0
  media 3 1 10
  boundary 10 42 42
end geom
'
read array 
  ara=1 nux=7 nuy=7 
  fill
  51  1   1   1   1   1   1 
  1   1   1   1   1   1   1 
  1   1   1   1   1   1   1 
  1   1   1   53  52  1   1 
  1   1   1   54  51  1   1 
  1   1   1   1   1   1   1 
  1   1   1   1   1   1   1  end fill
end array
'
read bnds
  all=mirror
end bnds
'
end model
end
