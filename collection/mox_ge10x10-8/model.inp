=t-depl parm=(addnux={{params.addnux}})
BWR-MOX GE 10x10-8, 2D full assembly model 
{{params.xsLib}}
' ----------------------------------------------------------------
'  template to generate libraries for ORIGEN 
'  parameters are: wtpt_inner      - wt% plutonium: inner
'                  wtpt_inedge     - wt% plutonium: inside edge
'                  wtpt_edge       - wt% plutonium: edge
'                  wtpt_corner     - wt% plutonium: corner
'                  wtpt_pu238      - wt% Pu238
'                  wtpt_pu239      - wt% Pu239
'                  wtpt_pu240      - wt% Pu240
'                  wtpt_pu241      - wt% Pu241
'                  wtpt_pu242      - wt% Pu242
'                  densityAm       - americium density (g/cc)
'                  coolant_density - coolant density (g/cc)
'                  dancoff1        - dancoff factor 1 (outer fuel pins)
'                  dancoff2        - dancoff factor 2 (Gd pins)
'                  power           - average specific power (MW/MTHM)
'                  burn            - depletion interval in days
' ----------------------------------------------------------------
' Documentation and Notes (empty fields are auto-populated):
'  [Change Log] 
'    Rev 0: Generated by J. Doe 
'    Rev 1: Generated by B. R. Betzler, June 2014 
'    Rev 2: Generated by B. R. Betzler, September 2015
'    Rev 3: Generated by S. Skutnik, August 2023
'  [Author(s)] B. R. Betzler
'  [SCALE Version] SCALE 6.3
'  [Reactor Type] Mixed Oxide Boiling Water Reactor General Electric 10x10-8
'  [Model Info] 2D t-depl full assembly model (see Figure \ref{fi:mox_ge10x10-8}), xsLib cross-section library
'  [Sources] 
'    1. B. J. Ade, ``Generation of Collapsed Cross Sections for Hatch 1 Cycles 1-3 and Generation of Generic BWR Reflector Cross Sections'', ORNL/LTR-2012/559, Oak Ridge National Laboratory, 2012. | 
'    2. H. Smith, J. Peterson, and J. Hu, ``Fuel Assembly Modeling for the Modeling and Simulation Toolset'', ORNL/LTR-2012-555 Rev. 1, Oak Ridge National Laboratory, 2013. | 
'    3. I. C. Gauld, ``MOX Cross-Section Libraries for ORIGEN-ARP'', ORNL/TM-2003/2, Oak Ridge National Laboratory, Oak Ridge, Tennessee, 2003. |
'    4. U. Mertyurek and I. C. Gauld, ``Development of ORIGEN Libraries for Mixed Oxide (MOX) Fuel Assembly Designs'', to be published, 2015.
'    5. H. Smith, J. Peterson, and J. Hu, ``Fuel Assembly Modeling for the Modeling and Simulation Toolset'', ORNL/LTR-2012-555 Rev. 1, Oak Ridge National Laboratory, 2013.
'  [Data Range]
'  [Libraries]
'  [Power]
'  [Other Info] 
'    Channel box data, fuel/gap/channel moderator densities, and temperatures from Reference 1.
'    All other dimensions, materials, etc. from Reference 2.
'    Gad layout altered according to best engineering judgement.
'    MOX isotopic vector information from Reference 3.
'    MOX zoning pattern from section 4.1 of Reference 4 (see Table 2, Eq.~3, and Eq.~4).
'    Specific power from Reference 5.
'  figure{mox_ge10x10-8.pdf: MOX BWR GE 10x10-8.}
' ----------------------------------------------------------------
read alias
  $fuelCorner 31 41 end
  $clad 2 12 22 32 42 52 end
  $cool 3 13 23 33 43 53 end
  $gap  4 14 24 34 44 54 end
  $fgad 311 312 313 314 315 316 317 end
end alias
'
' ----------------------------------------------------------------
read comp
'
' fuel inner
  uo2   1 {{fuelcomp.density_fuel}} {{100.0-fuelcomp.wtpt_inner}}  948.45 92235 0.7417 92238 99.2583 end
  puo2  1 {{fuelcomp.density_fuel}} {{fuelcomp.wtpt_inner}} 948.45 94238 {{fuelcomp.wtpt_pu238}}
                                     94239 {{fuelcomp.wtpt_pu239}}
                                     94240 {{fuelcomp.wtpt_pu240}}
                                     94241 {{fuelcomp.wtpt_pu241}}
                                     94242 {{fuelcomp.wtpt_pu242}} end
  am-241  1 den={{params.densityAm}} {{fuelcomp.wtpt_inner}} 948.45 end
'
' fuel inside edge
  uo2  11 {{fuelcomp.density_fuel}} {{100.0-fuelcomp.wtpt_inedge}}  948.45 92235 0.7417 92238 99.2583 end
  puo2 11 {{fuelcomp.density_fuel}} {{fuelcomp.wtpt_inedge}} 948.45 94238 {{fuelcomp.wtpt_pu238}}
                                      94239 {{fuelcomp.wtpt_pu239}}
                                      94240 {{fuelcomp.wtpt_pu240}}
                                      94241 {{fuelcomp.wtpt_pu241}}
                                      94242 {{fuelcomp.wtpt_pu242}} end
  am-241 11 den={{params.densityAm}} {{fuelcomp.wtpt_inedge}} 948.45 end
'
' fuel edge
  uo2  21 {{fuelcomp.density_fuel}} {{100.0-fuelcomp.wtpt_edge}} 948.45 92235 0.7417 92238 99.2583 end
  puo2 21 {{fuelcomp.density_fuel}} {{fuelcomp.wtpt_edge}} 948.45 94238 {{fuelcomp.wtpt_pu238}}
                                     94239 {{fuelcomp.wtpt_pu239}}
                                     94240 {{fuelcomp.wtpt_pu240}}
                                     94241 {{fuelcomp.wtpt_pu241}}
                                     94242 {{fuelcomp.wtpt_pu242}} end
  am-241 21 den={{params.densityAm}} {{fuelcomp.wtpt_edge}} 948.45 end
'
' fuel corner
  uo2  $fuelCorner {{fuelcomp.density_fuel}} {{100.0-fuelcomp.wtpt_corner}}  948.45 92235 0.7417 92238 99.2583 end
  puo2 $fuelCorner {{fuelcomp.density_fuel}} {{fuelcomp.wtpt_corner}} 948.45 94238 {{fuelcomp.wtpt_pu238}}
                                              94239 {{fuelcomp.wtpt_pu239}}
                                              94240 {{fuelcomp.wtpt_pu240}}
                                              94241 {{fuelcomp.wtpt_pu241}}
                                              94242 {{fuelcomp.wtpt_pu242}} end
  am-241 $fuelCorner den={{params.densityAm}} {{fuelcomp.wtpt_corner}} 948.45 end
'
' fuel - with {{{{fuelcomp.wtpt_gd}}% Gd2O3
  gd2o3 $fgad {{fuelcomp.density_fuel}} {{fuelcomp.wtpt_gd/100.0}} 948.45 end 
  uo2   $fgad {{fuelcomp.density_fuel}} {{1.0-fuelcomp.wtpt_gd/100.0}} 948.45 92235 0.7417 92238 99.2583 end
'
' clad  
  zirc2 $clad 1 636.06 end
'
' coolant - water
  h2o   $cool den={{state.coolant_density}} 1 560.29 end
'
' gap - helium
  he-4  $gap den=0.00011 1 792.26 end
'
' moderator - water in rod/channel
  h2o   5 den=0.7373 1 560.29 end
'
' channel/flow tube 
  zirc2 6 1 560.29 end
'
end comp
'
' ----------------------------------------------------------------
read celldata
  latticecell squarepitch fueld=0.876  1
                          gapd= 0.894  4
                          cladd=1.026  2
                          pitch=1.295  3 end
  latticecell squarepitch fueld=0.876 11
                          gapd= 0.894 14
                          cladd=1.026 12
                          pitch=1.295 13 end
  latticecell squarepitch fueld=0.876 21
                          gapd= 0.894 24
                          cladd=1.026 22
                          pitch=1.295 23 end
  centrmdata dan2pitch={{fuelcomp.dancoff1}} end centrmdata
  latticecell squarepitch fueld=0.876 31
                          gapd= 0.894 34
                          cladd=1.026 32
                          pitch=1.295 33 end
  centrmdata dan2pitch={{fuelcomp.dancoff1}} end centrmdata
  latticecell squarepitch fueld=0.876 41
                          gapd= 0.894 44
                          cladd=1.026 42
                          pitch=1.295 43 end
  centrmdata dan2pitch={{fuelcomp.dancoff2}} end centrmdata
  multiregion cylindrical left_bdy=reflected right_bdy=white end
      311   0.16555
      312   0.23412
      313   0.28674
      314   0.33110
      315   0.37018
      316   0.40551
      317   0.43800
       54   0.44700
       52   0.51300
       53   0.73063
    end zone
end celldata
'
' ----------------------------------------------------------------
read depletion
  1 11 21 $fuelCorner flux $fgad
end depletion
'
' ----------------------------------------------------------------
read burndata
  {%- for pb in time.burndata %}
  power={{pb.power}} burn={{pb.burn}} down=0 end 
  {%- endfor %}
end burndata
'
' ----------------------------------------------------------------
read model
  BWR-MOX GE 10x10
'
read parm
  cmfd=1
  drawit=yes
  echo=yes
  epsinner=-1e-4 
  fillmix=5
  run=yes  
  xycmfd=4
end parm
'
read materials
  mix=1           pn=1 com='fuel - inner zone'       end
  mix=11          pn=1 com='fuel - inside edge zone' end
  mix=21          pn=1 com='fuel - edge zone'        end
  mix=2           pn=1 com='clad'                    end
  mix=3           pn=2 com='coolant'                 end
  mix=4           pn=0 com='gap'                     end
  mix=5           pn=2 com='moderator'               end
  mix=6           pn=1 com='channel tube'            end
  mix=$fuelCorner pn=1 com='fuel - corner zone'      end
  mix=$fgad       pn=1 com='fuel with {{fuelcomp.wtpt_gd}}% Gd2O3'    end
end materials
'
read geom
unit 1
com='fuel rod - inner zone'
  cylinder 10 0.438
  cylinder 20 0.447
  cylinder 30 0.513
  media 1 1 10
  media 4 1 20 -10
  media 2 1 30 -20
  boundary 30
unit 2
com='fuel rod - inside edge zone'
  cylinder 10 0.438
  cylinder 20 0.447
  cylinder 30 0.513
  media 11 1 10
  media  4 1 20 -10
  media  2 1 30 -20
  boundary 30
unit 3
com='fuel rod - edge zone - edge dancoff'
  cylinder 10 0.438
  cylinder 20 0.447
  cylinder 30 0.513
  media 21 1 10
  media  4 1 20 -10
  media  2 1 30 -20
  boundary 30
unit 4
com='fuel rod - corner zone - edge dancoff'
  cylinder 10 0.438
  cylinder 20 0.447
  cylinder 30 0.513
  media 31 1 10
  media  4 1 20 -10
  media  2 1 30 -20
  boundary 30
unit 5
com='fuel rod - corner zone - corner dancoff'
  cylinder 10 0.438
  cylinder 20 0.447
  cylinder 30 0.513
  media 41 1 10
  media  4 1 20 -10
  media  2 1 30 -20
  boundary 30
unit 6
com='fuel rod - with {{fuelcomp.wtpt_gd}}% Gd2O'
  cylinder  10 0.16555
  cylinder  20 0.23412
  cylinder  30 0.28674
  cylinder  40 0.33110
  cylinder  50 0.37018
  cylinder  60 0.40551
  cylinder  70 0.438
  cylinder  80 0.447
  cylinder  90 0.513
  media 311 1 10
  media 312 1 20 -10
  media 313 1 30 -20
  media 314 1 40 -30
  media 315 1 50 -40
  media 316 1 60 -50
  media 317 1 70 -60
  media   4 1 80 -70
  media   2 1 90 -80
  boundary 90
unit 7
com='water rod'
  cylinder 10 1.161
  cylinder 20 1.261
  media 5 1 10
  media 6 1 20 -10
  boundary 20
global unit 10
com='assembly'
  cuboid   10 14.32305 0.91695 14.32305 0.91695
  cuboid   20 14.52625 0.71375 14.52625 0.71375
  cuboid   30 15.24 0 15.24  0
' for rounded corners, bottom right to top left
  cylinder 11 0.9652 chord -x=1.88215 chord -y=1.88215 origin x=1.88215 y=1.88215 sides=16
  cylinder 12 1.1684 chord -x=1.88215 chord -y=1.88215 origin x=1.88215 y=1.88215 sides=16
  cuboid   13 1.88215  0.71375  1.88215  0.71375
  cylinder 21 0.9652 chord +x=13.35785 chord -y=1.88215 origin x=13.35785 y=1.88215 sides=16
  cylinder 22 1.1684 chord +x=13.35785 chord -y=1.88215 origin x=13.35785 y=1.88215 sides=16
  cuboid   23 14.52625 13.35785  1.88215  0.71375
  cylinder 31 0.9652 chord -x=1.88215 chord +y=13.35785 origin x=1.88215 y=13.35785 sides=16
  cylinder 32 1.1684 chord -x=1.88215 chord +y=13.35785 origin x=1.88215 y=13.35785 sides=16
  cuboid   33 1.88215  0.71375 14.52625 13.35785
  cylinder 41 0.9652 chord +x=13.35785 chord +y=13.35785 origin x=13.35785 y=13.35785 sides=16
  cylinder 42 1.1684 chord +x=13.35785 chord +y=13.35785 origin x=13.35785 y=13.35785 sides=16
  cuboid   43 14.52625 13.35785 14.52625 13.35785
' pin-by-pin construction
  hole 5 origin x= 1.79250 y= 1.79250
  hole 4 origin x= 3.08750 y= 1.79250
  hole 3 origin x= 4.38250 y= 1.79250
  hole 3 origin x= 5.67750 y= 1.79250
  hole 3 origin x= 6.97250 y= 1.79250
  hole 3 origin x= 8.26750 y= 1.79250
  hole 3 origin x= 9.56250 y= 1.79250
  hole 3 origin x=10.85750 y= 1.79250
  hole 4 origin x=12.15250 y= 1.79250
  hole 5 origin x=13.44750 y= 1.79250
  hole 4 origin x= 1.79250 y= 3.08750
  hole 6 origin x= 3.08750 y= 3.08750
  hole 2 origin x= 4.38250 y= 3.08750
  hole 2 origin x= 5.67750 y= 3.08750
  hole 6 origin x= 6.97250 y= 3.08750
  hole 2 origin x= 8.26750 y= 3.08750
  hole 6 origin x= 9.56250 y= 3.08750
  hole 2 origin x=10.85750 y= 3.08750
  hole 6 origin x=12.15250 y= 3.08750
  hole 4 origin x=13.44750 y= 3.08750
  hole 3 origin x= 1.79250 y= 4.38250
  hole 2 origin x= 3.08750 y= 4.38250
  hole 1 origin x= 4.38250 y= 4.38250
  hole 1 origin x= 5.67750 y= 4.38250
  hole 1 origin x= 6.97250 y= 4.38250
  hole 1 origin x= 8.26750 y= 4.38250
  hole 1 origin x= 9.56250 y= 4.38250
  hole 1 origin x=10.85750 y= 4.38250
  hole 2 origin x=12.15250 y= 4.38250
  hole 3 origin x=13.44750 y= 4.38250
  hole 3 origin x= 1.79250 y= 5.67750
  hole 2 origin x= 3.08750 y= 5.67750
  hole 1 origin x= 4.38250 y= 5.67750
  hole 1 origin x= 8.26750 y= 5.67750
  hole 6 origin x= 9.56250 y= 5.67750
  hole 1 origin x=10.85750 y= 5.67750
  hole 6 origin x=12.15250 y= 5.67750
  hole 3 origin x=13.44750 y= 5.67750
  hole 3 origin x= 1.79250 y= 6.97250
  hole 6 origin x= 3.08750 y= 6.97250
  hole 1 origin x= 4.38250 y= 6.97250
  hole 1 origin x= 8.26750 y= 6.97250
  hole 1 origin x= 9.56250 y= 6.97250
  hole 1 origin x=10.85750 y= 6.97250
  hole 2 origin x=12.15250 y= 6.97250
  hole 3 origin x=13.44750 y= 6.97250
  hole 3 origin x= 1.79250 y= 8.26750
  hole 2 origin x= 3.08750 y= 8.26750
  hole 1 origin x= 4.38250 y= 8.26750
  hole 1 origin x= 5.67750 y= 8.26750
  hole 1 origin x= 6.97250 y= 8.26750
  hole 1 origin x=10.85750 y= 8.26750
  hole 6 origin x=12.15250 y= 8.26750
  hole 3 origin x=13.44750 y= 8.26750
  hole 3 origin x= 1.79250 y= 9.56250
  hole 6 origin x= 3.08750 y= 9.56250
  hole 1 origin x= 4.38250 y= 9.56250
  hole 6 origin x= 5.67750 y= 9.56250
  hole 1 origin x= 6.97250 y= 9.56250
  hole 1 origin x=10.85750 y= 9.56250
  hole 2 origin x=12.15250 y= 9.56250
  hole 3 origin x=13.44750 y= 9.56250
  hole 3 origin x= 1.79250 y=10.85750
  hole 2 origin x= 3.08750 y=10.85750
  hole 1 origin x= 4.38250 y=10.85750
  hole 1 origin x= 5.67750 y=10.85750
  hole 1 origin x= 6.97250 y=10.85750
  hole 1 origin x= 8.26750 y=10.85750
  hole 1 origin x= 9.56250 y=10.85750
  hole 1 origin x=10.85750 y=10.85750
  hole 2 origin x=12.15250 y=10.85750
  hole 3 origin x=13.44750 y=10.85750
  hole 4 origin x= 1.79250 y=12.15250
  hole 6 origin x= 3.08750 y=12.15250
  hole 2 origin x= 4.38250 y=12.15250
  hole 6 origin x= 5.67750 y=12.15250
  hole 2 origin x= 6.97250 y=12.15250
  hole 6 origin x= 8.26750 y=12.15250
  hole 2 origin x= 9.56250 y=12.15250
  hole 2 origin x=10.85750 y=12.15250
  hole 6 origin x=12.15250 y=12.15250
  hole 4 origin x=13.44750 y=12.15250
  hole 5 origin x= 1.79250 y=13.44750
  hole 4 origin x= 3.08750 y=13.44750
  hole 3 origin x= 4.38250 y=13.44750
  hole 3 origin x= 5.67750 y=13.44750
  hole 3 origin x= 6.97250 y=13.44750
  hole 3 origin x= 8.26750 y=13.44750
  hole 3 origin x= 9.56250 y=13.44750
  hole 3 origin x=10.85750 y=13.44750
  hole 4 origin x=12.15250 y=13.44750
  hole 5 origin x=13.44750 y=13.44750
' water rods
  hole 7 origin x=8.915 y=8.915
  hole 7 origin x=6.325 y=6.325
  media 3 1 10 -13 -23 -33 -43
  media 3 1 13  11
  media 3 1 23  21
  media 3 1 33  31
  media 3 1 43  41
  media 6 1 20 -10 -13 -23 -33 -43
  media 6 1 13  12 -11
  media 6 1 23  22 -21
  media 6 1 33  32 -31
  media 6 1 43  42 -41
  media 5 1 30 -20 
  media 5 1 13 -12
  media 5 1 23 -22
  media 5 1 33 -32
  media 5 1 43 -42
  boundary 30 48 48
end geom
'
read bnds
  all=mirror
end bnds
'
end data
end
'
