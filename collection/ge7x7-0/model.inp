=t-depl parm=(addnux={{params.addnux}})
BWR GE 7x7-0, 2D full assembly model
{{params.xslib}}
' ----------------------------------------------------------------
'  template to generate libraries for ORIGEN-S 
'  parameters are: wtpt_u235       - wt% U235
'                  wtpt_u234       - wt% U234
'                  wtpt_u236       - wt% U236
'                  wtpt_u238       - wt% U238
'                  wtpd_gd         - wt% Gd2O3
'                  coolant_density - coolant density (g/cc)
'                  fuel_density    - fuel density (g/cc)
'                  fuel_density_gd - Gd2O3 fuel density (g/cc)
'                  dancoff1        - dancoff factor, UOX rods
'                  dancoff2        - dancoff factor, Gd2O3-UOX rods
'                  specpow         - average specific power
'                  daystoburn      - depletion interval in days
' ----------------------------------------------------------------
' Documentation and Notes (empty fields are auto-populated):
'  [Change Log] 
'    Rev 0: Generated by J. Doe | 
'    Rev 1: Generated by B. R. Betzler, June 2014
'    Rev 2: Updated and generated by B. D. Hiscox, August 2021
'  [Author(s)] B. R. Betzler
'  [SCALE Version] SCALE 6.3
'  [Reactor Type] Boiling Water Reactor General Electric 7x7-0
'  [Model Info] 2D t-depl full assembly model (see Figure \ref{fi:ge7x7-0}), {{params.xslib}} cross-section library
'  [Sources] 
'    1. B. J. Ade, ``Generation of Collapsed Cross Sections for Hatch 1 Cycles 1-3 and Generation of Generic BWR Reflector Cross Sections'', ORNL/LTR-2012/559, Oak Ridge National Laboratory, 2012. |
'    2. H. Smith, J. Peterson, and J. Hu, ``Fuel Assembly Modeling for the Modeling and Simulation Toolset'', ORNL/LTR-2012-555 Rev. 1, Oak Ridge National Laboratory, 2013.
'  [Data Range]
'  [Libraries]
'  [Power]
'  [Other Info] All information from Reference 1. 
'    Specific power from Reference 2.
'  figure{ge7x7-0.pdf: BWR GE 7x7-0 fuel assembly.}
' ----------------------------------------------------------------
read alias
  $fuel 1 11 21 end
  $clad 2 12 22 32 end
  $cool 3 13 23 33 end
  $gap  4 14 24 34 end
  $fgad 311 312 313 314 315 316 317 end
end alias
'
' ----------------------------------------------------------------
read comp
'
' fuel
  uo2 $fuel den={{fuelcomps.fuel_density}} 1 948.45 92234 {{fuelcomp.wtpt_u234}}
                               92235 {{fuelcomp.wtpt_u235}}
                               92236 {{fuelcomp.wtpt_u236}}
                               92238 {{fuelcomp.wtpt_u238}} end
'
'
' fuel - with {{state.wtpt_gd}}% Gd2O3
  gd2o3 $fgad den={{fuelcomps.fuel_density_gd}} {{state.wtpt_gd/100.0} 948.45 end
  uo2   $fgad den={{fuelcomps.fuel_density_gd}} {{1.0-state.wtpt_gd/100.0}} 948.45 92234 {{fuelcomp.wtpt_u234}}
                                    92235 {{fuelcomp.wtpt_u235}}
                                    92236 {{fuelcomp.wtpt_u236}}
                                    92238 {{fuelcomp.wtpt_u238}} end
'
' clad  
  zirc2 $clad 1 636.06 end
'
' coolant - water
  h2o $cool den={{state.coolant_density}} 1 560.29 end
'
' gap - helium
  he-4  $gap den=0.00011 1 792.26 end
'
' moderator - water in rod/channel
  h2o    5 den=0.7373 1 560.29 end
'
' channel/flow tube
  zirc4  6 1 560.29 end
'
end comp
'
' ----------------------------------------------------------------
read celldata
  latticecell squarepitch fueld=1.21158  1
                          gapd= 1.24206  4
                          cladd=1.43002  2
                          pitch=1.87452  3 end
  centrmdata alump=0 pmc_dilute=1e30 pmc_omit=0 end centrmdata
  latticecell squarepitch fueld=1.21158 11
                          gapd= 1.24206 14
                          cladd=1.43002 12
                          pitch=1.87452 13 end
  centrmdata alump=0 pmc_dilute=1e30 pmc_omit=0 dan2pitch={{dynamic.dancoff1}} end centrmdata
  latticecell squarepitch fueld=1.21158 21
                          gapd= 1.24206 24 
                          cladd=1.43002 22
                          pitch=1.87452 23 end
  centrmdata alump=0 pmc_dilute=1e30 pmc_omit=0 dan2pitch={{dynamic.dancoff2}} end centrmdata
  multiregion cylindrical left_bdy=reflected right_bdy=white end
      311   0.22897
      312   0.32381
      313   0.39658
      314   0.45793
      315   0.51199
      316   0.56085
      317   0.60579
       34   0.62103
       32   0.71501
       33   1.05758
    end zone
  centrmdata alump=0 pmc_dilute=1e30 pmc_omit=0 end centrmdata
end celldata
'
' ----------------------------------------------------------------
read depletion
  $fuel flux $fgad
end depletion
'
' ----------------------------------------------------------------
read burndata
  {%- for pb in time.burndata %}
  power={{pb.power}} burn={{pb.burn}} down=0 end
  {%- endfor %}
end burndata
'
' ----------------------------------------------------------------
read model
  BWR GE 7x7
'
read parm
  cmfd=1
  drawit=yes
  echo=yes
  epsinner=-1e-4   
  fillmix=5
  run=yes 
  xycmfd=4
end parm
'
read materials
  mix=$fuel pn=1 com='fuel'         end
  mix=2     pn=1 com='clad'         end
  mix=3     pn=2 com='coolant'      end
  mix=4     pn=0 com='gap'          end
  mix=5     pn=2 com='moderator'    end
  mix=6     pn=1 com='channel tube' end
  mix=$fgad pn=1 com='fuel with 4.0% Gd2O3' end
end materials
'
read geom
unit 1
com='fuel rod'
  cylinder 10   0.60579
  cylinder 20   0.62103
  cylinder 30   0.71501
  media 1 1 10
  media 4 1 20 -10
  media 2 1 30 -20
  boundary 30
unit 2
com='fuel rod - edge'
  cylinder 10   0.60579
  cylinder 20   0.62103
  cylinder 30   0.71501
  media 11 1 10
  media  4 1 20 -10
  media  2 1 30 -20
  boundary 30
unit 3
com='fuel rod - corner'
  cylinder 10   0.60579
  cylinder 20   0.62103
  cylinder 30   0.71501
  media 21 1 10
  media  4 1 20 -10
  media  2 1 30 -20
  boundary 30
unit 4
com='fuel rod - with 4.0% Gd2O3'
  cylinder  10 0.22897
  cylinder  20 0.32381
  cylinder  30 0.39658
  cylinder  40 0.45793
  cylinder  50 0.51199
  cylinder  60 0.56085
  cylinder  70 0.60579
  cylinder  80 0.62103
  cylinder  90 0.71501
  media 311 1  10
  media 312 1  20  -10
  media 313 1  30  -20
  media 314 1  40  -30
  media 315 1  50  -40
  media 316 1  60  -50
  media 317 1  70  -60
  media   4 1  80  -70
  media   2 1  90  -80
  boundary 90
global unit 10
com='assembly'
  cuboid   10 14.32305 0.91695 14.32305 0.91695
  cuboid   20 14.52625 0.71375 14.52625 0.71375
  cuboid   30 15.24 0 15.24  0
' for rounded corners, bottom right to top left
  cylinder 11 0.9652 chord -x=1.88215 chord -y=1.88215 origin x=1.88215 y=1.88215 sides=18
  cylinder 12 1.1684 chord -x=1.88215 chord -y=1.88215 origin x=1.88215 y=1.88215 sides=18
  cuboid   13 1.88215  0.71375  1.88215  0.71375
  cylinder 21 0.9652 chord +x=13.35785 chord -y=1.88215 origin x=13.35785 y=1.88215 sides=18
  cylinder 22 1.1684 chord +x=13.35785 chord -y=1.88215 origin x=13.35785 y=1.88215 sides=18
  cuboid   23 14.52625 13.35785  1.88215  0.71375
  cylinder 31 0.9652 chord -x=1.88215 chord +y=13.35785 origin x=1.88215 y=13.35785 sides=18
  cylinder 32 1.1684 chord -x=1.88215 chord +y=13.35785 origin x=1.88215 y=13.35785 sides=18
  cuboid   33 1.88215  0.71375 14.52625 13.35785
  cylinder 41 0.9652 chord +x=13.35785 chord +y=13.35785 origin x=13.35785 y=13.35785 sides=18
  cylinder 42 1.1684 chord +x=13.35785 chord +y=13.35785 origin x=13.35785 y=13.35785 sides=18
  cuboid   43 14.52625 13.35785 14.52625 13.35785
' pin-by-pin construction
  hole 3 origin x= 1.99644 y= 1.99644
  hole 2 origin x= 3.87096 y= 1.99644
  hole 2 origin x= 5.74548 y= 1.99644
  hole 2 origin x= 7.62000 y= 1.99644
  hole 2 origin x= 9.49452 y= 1.99644
  hole 2 origin x=11.36904 y= 1.99644
  hole 3 origin x=13.24356 y= 1.99644
  hole 2 origin x= 1.99644 y= 3.87096
  hole 1 origin x= 3.87096 y= 3.87096
  hole 4 origin x= 5.74548 y= 3.87096
  hole 1 origin x= 7.62000 y= 3.87096
  hole 1 origin x= 9.49452 y= 3.87096
  hole 1 origin x=11.36904 y= 3.87096
  hole 2 origin x=13.24356 y= 3.87096
  hole 2 origin x= 1.99644 y= 5.74548
  hole 1 origin x= 3.87096 y= 5.74548
  hole 1 origin x= 5.74548 y= 5.74548
  hole 1 origin x= 7.62000 y= 5.74548
  hole 4 origin x= 9.49452 y= 5.74548
  hole 1 origin x=11.36904 y= 5.74548
  hole 2 origin x=13.24356 y= 5.74548
  hole 2 origin x= 1.99644 y= 7.62000
  hole 1 origin x= 3.87096 y= 7.62000
  hole 1 origin x= 5.74548 y= 7.62000
  hole 1 origin x= 7.62000 y= 7.62000
  hole 1 origin x= 9.49452 y= 7.62000
  hole 1 origin x=11.36904 y= 7.62000
  hole 2 origin x=13.24356 y= 7.62000
  hole 2 origin x= 1.99644 y= 9.49452
  hole 1 origin x= 3.87096 y= 9.49452
  hole 1 origin x= 5.74548 y= 9.49452
  hole 1 origin x= 7.62000 y= 9.49452
  hole 1 origin x= 9.49452 y= 9.49452
  hole 4 origin x=11.36904 y= 9.49452
  hole 2 origin x=13.24356 y= 9.49452
  hole 2 origin x= 1.99644 y=11.36904
  hole 1 origin x= 3.87096 y=11.36904
  hole 1 origin x= 5.74548 y=11.36904
  hole 1 origin x= 7.62000 y=11.36904
  hole 1 origin x= 9.49452 y=11.36904
  hole 1 origin x=11.36904 y=11.36904
  hole 2 origin x=13.24356 y=11.36904
  hole 3 origin x= 1.99644 y=13.24356
  hole 2 origin x= 3.87096 y=13.24356
  hole 2 origin x= 5.74548 y=13.24356
  hole 2 origin x= 7.62000 y=13.24356
  hole 2 origin x= 9.49452 y=13.24356
  hole 2 origin x=11.36904 y=13.24356
  hole 3 origin x=13.24356 y=13.24356
  media 3 1 10 -13 -23 -33 -43
  media 3 1 13  11
  media 3 1 23  21
  media 3 1 33  31
  media 3 1 43  41
  media 6 1 20 -10 -13 -23 -33 -43
  media 6 1 13  12 -11
  media 6 1 23  22 -21
  media 6 1 33  32 -31
  media 6 1 43  42 -41
  media 5 1 30 -20 
  media 5 1 13 -12
  media 5 1 23 -22
  media 5 1 33 -32
  media 5 1 43 -42
  boundary 30 42 42
end geom
'
read bnds
  all=mirror
end bnds
'
end model
end
