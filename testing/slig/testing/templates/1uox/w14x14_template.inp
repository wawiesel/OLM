=t-depl parm=(centrm, addnux=4)
PWR Westinghouse 14x14, 2D full assembly model
v7-252
' ----------------------------------------------------------------
'  template to generate libraries for ORIGEN-S 
'  parameters are: u235wt%       - wt% U235
'                  u234wt%       - wt% U234
'                  u236wt%       - wt% U236
'                  u238wt%       - wt% U238
'                  ddd           - coolant density (g/cc)
'                  namelibrary   - name of generated ORIGEN library
'                  specpow       - average specific power
'                  daystoburn    - depletion interval in days
'  options are:    name          - w14_
'                  enrichment    - 0.5, 1.5, 2.0, 3.0, 4.0, 5.0, 6.0,
'                                  7.0, 8.0, 8.5
'                  cool. density - 0.7264
'                  spec. power   - 40.0
'                  burnups       - 0, 2, 4, 6, 9, 12, 15, 18, 21,
'                                  24, 27, 30, 33, 36, 39, 42, 45,
'                                  48, 51, 54, 57, 60, 63, 66, 69,
'                                  72, 75, 78, 81, 84
' ----------------------------------------------------------------
' Documentation and Notes (empty fields are auto-populated):
'  [Change Log] 
'    Rev 0: Generated by J. Doe | 
'    Rev 1: Generated by B. R. Betzler, June 2014
'    Rev 2: Updated and generated by B. D. Hiscox, August 2021
'  [Author(s)] B. R. Betzler
'  [SCALE Version] SCALE 6.3
'  [Reactor Type] Pressurized Water Reactor Westinghouse 14x14
'  [Model Info] 2D t-depl full assembly model, v7-252 cross-section library (see Figure \ref{fi:w14x14}), v7-252 cross-section library
'  [Sources] 
'    1. ``Characteristics of Spent Fuel, High-Level Waste, and other Radioactive Wastes which May Require Long-Term Isolation'', Appendix 2A. Physical Descriptions of LWR Fuel Assemblies, DOE/RW-0184, Volume 3 of 6, U.S. DOE Office of Civilian Radioactive Waste Management, 1987. | 
'    2. SCALE: A Comprehensive Modeling and Simulation Suite for Nuclear Safety Analysis and Design, ORNL/TM-2005/39, Version 6.1, Oak Ridge National Laboratory, Oak Ridge, Tennessee, June 2011. |
'    3. H. Smith, J. Peterson, and J. Hu, ``Fuel Assembly Modeling for the Modeling and Simulation Toolset'', ORNL/LTR-2012-555 Rev. 1, Oak Ridge National Laboratory, 2013.
'  [Data Range]
'  [Libraries]
'  [Power]
'  [Other Info] 
'    Fuel density, gap gas pressure from Appendix 2A of Reference 1. 
'    Temperatures, moderator density, boron concentration from Table D1.A.2 of Reference 2. 
'    All other dimensions, materials, etc. from Reference 3.
'  figure{w14x14.pdf: PWR Westinghouse 14x14 fuel assembly.}
' ----------------------------------------------------------------
read comp
'
' fuel - 91-95% theoretical density (10.96g/cc) (set to max)
  uo2   1 den=10.412 1 923 92234 u234wt% 
                           92235 u235wt% 
                           92236 u236wt% 
                           92238 u238wt% end 
'
' clad
  zirc4 2 1 595 end 
'
' coolant - water with 653 ppm soluble boron 
  h2o   3 den=ddd 1.000000 573.4 end
  boron 3 den=ddd 0.000653 573.4 end
'
' gap - 300-450psig (set at minimum)
  he-4  4 den=0.001672 1 595 end
'
' guide tube
  zirc4 5 1 573.4 end 
'
end comp
'
' ----------------------------------------------------------------
read celldata
  latticecell squarepitch fueld=0.929386 1 
                          gapd= 0.948436 4
                          cladd=1.07188  2
                          pitch=1.43002  3 end
  centrmdata alump=0 pmc_dilute=1e30 pmc_omit=0 end centrmdata 
end celldata
'
' ----------------------------------------------------------------
read depletion 
  1 
end depletion
'
' ----------------------------------------------------------------
read burndata
  power=specpow burn=daystoburn down=0 end
end burndata
'
' ----------------------------------------------------------------
read model
  PWR Westinghouse 14x14  
'
read parm
  cmfd=1
  drawit=yes
  echo=yes
  epsinner=-1e-4 
  fillmix=3
  run=yes   
  xycmfd=4
  sn=8
  cell_tol=1e-8
end parm
'
read materials
  mix=1 pn=1 com='fuel'       end
  mix=2 pn=1 com='clad'       end
  mix=3 pn=2 com='coolant'    end
  mix=4 pn=0 com='gap'        end
  mix=5 pn=1 com='guide tube' end
end materials
'
read geom
unit 1 
com='fuel rod'
  cylinder 10    0.464693
  cylinder 20    0.474218 
  cylinder 30    0.53594
  cuboid   40  4p0.71501
  media 1 1 10
  media 4 1 20 -10
  media 2 1 30 -20
  media 3 1 40 -30
  boundary  40
unit 5
com='guide tube'
  cylinder 10    0.64135
  cylinder 20    0.68453
  cuboid   40  4p0.71501
  media 3 1 10
  media 5 1 20 -10
  media 3 1 40 -20
  boundary  40
unit 6
com='instrument tube'
  cylinder 10    0.64135
  cylinder 20    0.68453
  cuboid   40  4p0.71501
  media 3 1 10
  media 5 1 20 -10
  media 3 1 40 -20
  boundary  40
global unit 10
com='full assembly'
  cuboid 10 20.10664 0.0 20.10664 0.0 
  array 1 10 place 1 1 0.75819 0.75819
  media 3 1 10
  boundary 10 84 84
end geom
'
read array
  ara=1 nux=14 nuy=14 typ=cuboidal 
  fill
  1  1  1  1  1  1  1  1  1  1  1  1  1  1
  1  1  1  1  1  1  1  1  1  1  1  1  1  1
  1  1  5  1  1  5  1  1  5  1  1  5  1  1
  1  1  1  1  1  1  1  1  1  1  1  1  1  1
  1  1  1  1  5  1  1  1  1  5  1  1  1  1
  1  1  5  1  1  1  1  1  1  1  1  5  1  1
  1  1  1  1  1  1  1  6  1  1  1  1  1  1
  1  1  1  1  1  1  1  1  1  1  1  1  1  1
  1  1  5  1  1  1  1  1  1  1  1  5  1  1
  1  1  1  1  5  1  1  1  1  5  1  1  1  1
  1  1  1  1  1  1  1  1  1  1  1  1  1  1
  1  1  5  1  1  5  1  1  5  1  1  5  1  1
  1  1  1  1  1  1  1  1  1  1  1  1  1  1
  1  1  1  1  1  1  1  1  1  1  1  1  1  1  end fill
end array
'
read bnds 
  all=mirror
end bnds
'
end model
end
'
