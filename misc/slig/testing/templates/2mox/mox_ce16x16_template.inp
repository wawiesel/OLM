=t-depl parm=(centrm, addnux=4)
PWR CE 16x16, 2D quarter assembly model
v7-252
' ----------------------------------------------------------------
'  template to generate libraries for ORIGEN-S 
'  parameters are: IcontentPu    - wt% plutonium: inner
'                  IcontentU     - wt% uranium: inner
'                  IEcontentPu   - wt% plutonium: inside edge
'                  IEcontentU    - wt% uranium: inside edge
'                  EcontentPu    - wt% plutonium: edge
'                  EcontentU     - wt% uranium: edge
'                  CcontentPu    - wt% plutonium: corner
'                  CcontentU     - wt% uranium: corner
'                  pu238wt%      - wt% Pu238
'                  pu239wt%      - wt% Pu239
'                  pu240wt%      - wt% Pu240
'                  pu241wt%      - wt% Pu241
'                  pu242wt%      - wt% Pu242
'                  densityAm     - americium density (g/cc)
'                  ddd           - coolant density (g/cc)
'                  namelibrary   - name of generated ORIGEN library
'                  specpow       - average specific power
'                  daystoburn    - depletion interval in days
'  options are:    name          - mox_ce16_
'                  pu content    - 4.0, 7.0, 10.0
'                  pu vector     - 50.0, 55.0, 60.0, 65.0, 70.0
'                  cool. density - 0.71
'                  spec. power   - 40.0
'                  burnups       - 0, 2, 4, 6, 9, 12, 15, 18, 21,
'                                  24, 27, 30, 33, 36, 39, 42, 45,
'                                  48, 51, 54, 57, 60, 63, 66, 69,
'                                  72, 75, 78, 81, 84
'                  pin_zone      - 124, 52, 48, 12
'                  pin_gad       - 0
'                  avg_pin_dens. - 10.4
' ----------------------------------------------------------------
' Documentation and Notes (empty fields are auto-populated):
'  [Change Log] 
'    Rev 0: Generated by J. Doe | 
'    Rev 1: Generated by B. R. Betzler, June 2014 | 
'    Rev 2: Generated by B. R. Betzler, September 2015
'    Rev 3: Updated and generated by B. D. Hiscox, August 2021
'  [Author(s)] B. R. Betzler
'  [SCALE Version] SCALE 6.3
'  [Reactor Type] Mixed Oxide Pressurized Water Reactor Combustion Engineering 16x16
'  [Model Info] 2D t-depl quarter assembly model (see Figure \ref{fi:mox_ce16x16}), v7-252 cross-section library
'  [Sources] 
'    1. ``Characteristics of Spent Fuel, High-Level Waste, and other Radioactive Wastes which May Require Long-Term Isolation'', Appendix 2A. Physical Descriptions of LWR Fuel Assemblies, DOE/RW-0184, Volume 3 of 6, U.S. DOE Office of Civilian Radioactive Waste Management, 1987. | 
'    2. S. M. Bowman, I. C. Gauld, and J. C. Wagner, ``Recommendations on Fuel Parameters for Standard Technical Specifications for Spent Fuel Storage Casks'', NUREG/CR-6716 (ORNL/TM-2000/385), U.S. Nuclear Regulatory Commission, Oak Ridge National Laboratory, March 2001. | 
'    3. H. Smith, J. Peterson, and J. Hu, ``Fuel Assembly Modeling for the Modeling and Simulation Toolset'', ORNL/LTR-2012-555 Rev. 1, Oak Ridge National Laboratory, 2013. | 
'    4. I. C. Gauld, ``MOX Cross-Section Libraries for ORIGEN-ARP'', ORNL/TM-2003/2, Oak Ridge National Laboratory, Oak Ridge, Tennessee, 2003. |
'    5. U. Mertyurek and I. C. Gauld, ``Development of ORIGEN Libraries for Mixed Oxide (MOX) Fuel Assembly Designs'', to be published, 2015.
'  [Data Range]
'  [Libraries]
'  [Power]
'  [Other Info] 
'    Fuel density, gap gas pressure from Appendix 2A of Reference 1.
'    Temperatures, moderator density from Table 7 of Reference 2.
'    All other dimensions, materials, etc. from Reference 3. 
'    MOX isotopic vector information from Reference 4.
'    MOX zoning pattern from section 4.1 of Reference 5 (see Table 2, Eq.~3, and Eq.~4).
'  figure{mox_ce16x16.pdf: MOX PWR CE 16x16 fuel assembly.}
' ----------------------------------------------------------------
read alias
  $fuel 1 11 21 31 end
  $clad 2 12 22 32 end
  $mod  3 13 23 33 end
  $gap  4 14 24 34 end
end alias
'
' ----------------------------------------------------------------
read comp
'
' fuel inner
  uo2  1 den=10.4 IcontentU  1000 92234 0.001 92235 0.24 92238 99.759 end
  puo2 1 den=10.4 IcontentPu 1000 94238 pu238wt%
                                  94239 pu239wt%
                                  94240 pu240wt%
                                  94241 pu241wt%
                                  94242 pu242wt% end
  am-241 1 den=densityAm IcontentPu 1000 end
'
' fuel inside edge
  uo2  11 den=10.4 IEcontentU  1000 92234 0.001 92235 0.24 92238 99.759 end
  puo2 11 den=10.4 IEcontentPu 1000 94238 pu238wt%
                                    94239 pu239wt%
                                    94240 pu240wt%
                                    94241 pu241wt%
                                    94242 pu242wt% end
  am-241 11 den=densityAm IEcontentPu 1000 end
'
' fuel edge
  uo2  21 den=10.4 EcontentU  1000 92234 0.001 92235 0.24 92238 99.759 end
  puo2 21 den=10.4 EcontentPu 1000 94238 pu238wt%
                                   94239 pu239wt%
                                   94240 pu240wt%
                                   94241 pu241wt%
                                   94242 pu242wt% end
  am-241 21 den=densityAm EcontentPu 1000 end
'
' fuel corner
  uo2  31 den=10.4 CcontentU  1000 92234 0.001 92235 0.24 92238 99.759 end
  puo2 31 den=10.4 CcontentPu 1000 94238 pu238wt%
                                   94239 pu239wt%
                                   94240 pu240wt%
                                   94241 pu241wt%
                                   94242 pu242wt% end
  am-241 31 den=densityAm CcontentPu 1000 end
'
' clad
  zirc4 $clad 1 620 end 
'
' coolant - water with 650 ppm soluble boron 
  h2o   $mod den=ddd 1.000000 600 end
  boron $mod den=ddd 0.000650 600 end
'
' gap - 300-450psig (set at minimum)
  he-4  $gap den=0.001605 1 620 end
'
' guide tube
  zirc4 5 1 600 end 
end comp
'
' ----------------------------------------------------------------
read celldata
  latticecell squarepitch fueld=0.8255  $fuel
                          gapd= 0.84328 $gap
                          cladd=0.97028 $clad
                          pitch=1.286   $mod  end
Â  centrmdata alump=0 pmc_dilute=1e30 pmc_omit=0 end centrmdata
end celldata
'
' ----------------------------------------------------------------
read depletion 
  $fuel
end depletion
'
' ----------------------------------------------------------------
read burndata
  power=specpow burn=daystoburn down=0 end
end burndata
'
' ----------------------------------------------------------------
read model
  PWR CE 16x16  
'
read parm
  cmfd=1
  drawit=yes
  echo=yes
  epsinner=-1e-4 
  fillmix=3 
  run=yes
  xycmfd=4
  sn=8 
  cell_tol=1e-8
end parm
'
read materials
  mix=1  pn=1 com='fuel - inner zone'       end
  mix=2  pn=1 com='clad'                    end
  mix=3  pn=2 com='coolant'                 end
  mix=4  pn=0 com='gap'                     end
  mix=5  pn=1 com='guide tube'              end
  mix=11 pn=1 com='fuel - inside edge zone' end
  mix=21 pn=1 com='fuel - edge zone'        end
  mix=31 pn=1 com='fuel - corner zone'      end
end materials
'
read geom
unit 1
com='fuel rod - inner zone'
  cylinder 10   0.41275 
  cylinder 20   0.42164 
  cylinder 30   0.48514
  cuboid   40 4p0.643
  media 1 1 10
  media 4 1 20 -10
  media 2 1 30 -20 
  media 3 1 40 -30
  boundary 40
unit 2
com='fuel rod - inside edge zone'
  cylinder 10   0.41275 
  cylinder 20   0.42164 
  cylinder 30   0.48514
  cuboid   40 4p0.643
  media 11 1 10
  media  4 1 20 -10
  media  2 1 30 -20 
  media  3 1 40 -30
  boundary 40
unit 3
com='fuel rod - edge zone'
  cylinder 10   0.41275 
  cylinder 20   0.42164 
  cylinder 30   0.48514
  cuboid   40 4p0.643
  media 21 1 10
  media  4 1 20 -10
  media  2 1 30 -20 
  media  3 1 40 -30
  boundary 40
unit 4
com='fuel rod - corner zone'
  cylinder 10   0.41275 
  cylinder 20   0.42164 
  cylinder 30   0.48514
  cuboid   40 4p0.643
  media 31 1 10
  media  4 1 20 -10
  media  2 1 30 -20 
  media  3 1 40 -30
  boundary 40
unit 51
com='1/4 guide tube - NE'
  cylinder 10 1.143  chord +x=0 chord +y=0
  cylinder 20 1.2446 chord +x=0 chord +y=0
  cuboid   30 1.286  0 1.286 0
  media 3 1 10
  media 5 1 20 -10
  media 3 1 30 -20 
  boundary 30
unit 52
com='1/4 guide tube - SE'
  cylinder 10 1.143  origin x=0 y=1.286 chord +x=0 chord -y=1.286
  cylinder 20 1.2446 origin x=0 y=1.286 chord +x=0 chord -y=1.286
  cuboid   30 1.286  0 1.286 0
  media 3 1 10
  media 5 1 20 -10
  media 3 1 30 -20 
  boundary 30
unit 53
com='1/4 guide tube - SW'
  cylinder 10 1.143  origin x=1.286 y=1.286 chord -x=1.286 chord -y=1.286
  cylinder 20 1.2446 origin x=1.286 y=1.286 chord -x=1.286 chord -y=1.286
  cuboid   30 1.286  0 1.286 0
  media 3 1 10
  media 5 1 20 -10
  media 3 1 30 -20 
  boundary 30
unit 54
com='1/4 guide tube - NW'
  cylinder 10 1.143  origin x=1.286 y=0 chord -x=1.286 chord +y=0
  cylinder 20 1.2446 origin x=1.286 y=0 chord -x=1.286 chord +y=0
  cuboid   30 1.286  0 1.286 0
  media 3 1 10
  media 5 1 20 -10
  media 3 1 30 -20 
  boundary 30
global unit 10
com='1/4 assembly - NE'
  cuboid 10 10.39 0 10.39 0
  array 1 10 place 1 1 0 0
  media 3 1 10
  boundary 10 48 48 
end geom
'
read array 
  ara=1 nux=8 nuy=8 
  fill
  51  1   1   1   1   1   2   3
  1   1   1   1   1   1   2   3
  1   1   1   1   1   1   2   3
  1   1   1   53  52  1   2   3
  1   1   1   54  51  1   2   3
  1   1   1   1   1   1   2   3
  2   2   2   2   2   2   2   4
  3   3   3   3   3   3   4   4  end fill
end array
'
read bnds
  all=mirror
end bnds
'
end model
end
'
