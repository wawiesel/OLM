=t-depl parm=(centrm, addnux=4)
PWR Westinghouse OFA 17x17, 2D quarter assembly model 
v7-252
' ----------------------------------------------------------------
'  template to generate libraries for ORIGEN-S 
'  parameters are: IcontentPu    - wt% plutonium: inner
'                  IcontentU     - wt% uranium: inner
'                  IEcontentPu   - wt% plutonium: inside edge
'                  IEcontentU    - wt% uranium: inside edge
'                  EcontentPu    - wt% plutonium: edge
'                  EcontentU     - wt% uranium: edge
'                  CcontentPu    - wt% plutonium: corner
'                  CcontentU     - wt% uranium: corner
'                  pu238wt%      - wt% Pu238
'                  pu239wt%      - wt% Pu239
'                  pu240wt%      - wt% Pu240
'                  pu241wt%      - wt% Pu241
'                  pu242wt%      - wt% Pu242
'                  densityAm     - americium density (g/cc)
'                  ddd           - coolant density (g/cc)
'                  namelibrary   - name of generated ORIGEN library
'                  specpow       - average specific power
'                  daystoburn    - depletion interval in days
'  options are:    name          - mox_w17_ofa_
'                  pu content    - 4.0, 7.0, 10.0
'                  pu vector     - 50.0, 55.0, 60.0, 65.0, 70.0
'                  cool. density - 0.71
'                  spec. power   - 40.0
'                  burnups       - 0, 2, 4, 6, 9, 12, 15, 18, 21,
'                                  24, 27, 30, 33, 36, 39, 42, 45,
'                                  48, 51, 54, 57, 60, 63, 66, 69,
'                                  72, 75, 78, 81, 84
'                  pin_zone      - 144, 56, 52, 12
'                  pin_gad       - 0
'                  avg_pin_dens. - 10.4
' ----------------------------------------------------------------
' Documentation and Notes (empty fields are auto-populated):
'  [Change Log] 
'    Rev 0: Generated by J. Doe | 
'    Rev 1: Generated by B. R. Betzler, June 2014 | 
'    Rev 2: Generated by B. R. Betzler, September 2015
'    Rev 3: Updated and generated by B. D. Hiscox, August 2021
'  [Author(s)] B. R. Betzler
'  [SCALE Version] SCALE 6.3
'  [Reactor Type] Mixed Oxide Pressurized Water Reactor Westinghouse OFA 17x17
'  [Model Info] 2D t-depl quarter assembly model (see Figure \ref{fi:mox_w17x17_ofa}), v7-252 cross-section library
'  [Sources] 
'    1. ``Characteristics of Spent Fuel, High-Level Waste, and other Radioactive Wastes which May Require Long-Term Isolation'', Appendix 2A. Physical Descriptions of LWR Fuel Assemblies, DOE/RW-0184, Volume 3 of 6, U.S. DOE Office of Civilian Radioactive Waste Management, 1987. | 
'    2. SCALE: A Comprehensive Modeling and Simulation Suite for Nuclear Safety Analysis and Design, ORNL/TM-2005/39, Version 6.1, Oak Ridge National Laboratory, Oak Ridge, Tennessee, June 2011. |
'    3. J. C. Wagner and C. V. Parks, ``Parametric Study of the Effect of Burnable Poison Rods for PWR Burnup Credit'', NUREG/CR-6761 (ORNL/TM-2000/373), U.S. Nuclear Regulatory Commission, Oak Ridge National Laboratory, March 2002. | 
'    4. H. Smith, J. Peterson, and J. Hu, ``Fuel Assembly Modeling for the Modeling and Simulation Toolset'', ORNL/LTR-2012-555 Rev. 1, Oak Ridge National Laboratory, 2013. | 
'    5. I. C. Gauld, ``MOX Cross-Section Libraries for ORIGEN-ARP'', ORNL/TM-2003/2, Oak Ridge National Laboratory, Oak Ridge, Tennessee, 2003. |
'    6. U. Mertyurek and I. C. Gauld, ``Development of ORIGEN Libraries for Mixed Oxide (MOX) Fuel Assembly Designs'', to be published, 2015.
'  [Data Range]
'  [Libraries]
'  [Power]
'  [Other Info] 
'    Fuel density, gap gas pressure from Appendix 2A of Reference 1.
'    Moderator temperature, density from Table D1.A.2 of Reference 2.
'    Other temperatures from Table 7 of Reference 3.
'    All other dimensions, materials, etc. from Reference 4.
'    MOX isotopic vector information from Reference 5.
'    MOX zoning pattern from section 4.1 of Reference 6 (see Table 2, Eq.~3, and Eq.~4).
'  figure{mox_w17x17_ofa.pdf: MOX PWR Westinghouse OFA 17x17 fuel assembly.}
' ----------------------------------------------------------------
read alias
  $fuel 1 11 21 31 end
  $clad 2 12 22 32 end
  $mod  3 13 23 33 end
  $gap  4 14 24 34 end
end alias
'
' ----------------------------------------------------------------
read comp
'
' fuel inner
  uo2  1 den=10.4 IcontentU  1000 92234 0.001 92235 0.24 92238 99.759 end
  puo2 1 den=10.4 IcontentPu 1000 94238 pu238wt%
                                  94239 pu239wt%
                                  94240 pu240wt%
                                  94241 pu241wt%
                                  94242 pu242wt% end
  am-241 1 den=densityAm IcontentPu 1000 end
'
' fuel inside edge
  uo2  11 den=10.4 IEcontentU  1000 92234 0.001 92235 0.24 92238 99.759 end
  puo2 11 den=10.4 IEcontentPu 1000 94238 pu238wt%
                                    94239 pu239wt%
                                    94240 pu240wt%
                                    94241 pu241wt%
                                    94242 pu242wt% end
  am-241 11 den=densityAm IEcontentPu 1000 end
'
' fuel edge
  uo2  21 den=10.4 EcontentU  1000 92234 0.001 92235 0.24 92238 99.759 end
  puo2 21 den=10.4 EcontentPu 1000 94238 pu238wt%
                                   94239 pu239wt%
                                   94240 pu240wt%
                                   94241 pu241wt%
                                   94242 pu242wt% end
  am-241 21 den=densityAm EcontentPu 1000 end
'
' fuel corner
  uo2  31 den=10.4 CcontentU  1000 92234 0.001 92235 0.24 92238 99.759 end
  puo2 31 den=10.4 CcontentPu 1000 94238 pu238wt%
                                   94239 pu239wt%
                                   94240 pu240wt%
                                   94241 pu241wt%
                                   94242 pu242wt% end
  am-241 31 den=densityAm CcontentPu 1000 end
'
' clad
  zirc4 $clad 1 620 end 
'
' coolant - water with 650 ppm soluble boron 
  h2o   $mod den=ddd 1.000000 582 end
  boron $mod den=ddd 0.000650 582 end
'
' gap - 275-350psig (set at minimum)
  he-4  $gap den=0.001471 1 620 end
'
' guide tube
  zirc4 5 1 582 end 
'
end comp
'
' ----------------------------------------------------------------
read celldata
  latticecell squarepitch fueld=0.784352 $fuel
                          gapd= 0.8001   $gap
                          cladd=0.9144   $clad
                          pitch=1.25984  $mod  end
Â  centrmdata alump=0 pmc_dilute=1e30 pmc_omit=0 end centrmdata
end celldata
'
' ----------------------------------------------------------------
read depletion 
  $fuel
end depletion
'
' ----------------------------------------------------------------
read burndata
  power=specpow burn=daystoburn down=0 end
end burndata
'
' ----------------------------------------------------------------
read model
  PWR Westinghouse OFA 17x17  
'
read parm
  cmfd=1
  drawit=yes
  echo=yes
  epsinner=-1e-4
  fillmix=3
  run=yes
  xycmfd=4
  sn=8
  cell_tol=1e-8
end parm
'
read materials
  mix=1  pn=1 com='fuel - inner zone'       end
  mix=2  pn=1 com='clad'                    end
  mix=3  pn=2 com='coolant'                 end
  mix=4  pn=0 com='gap'                     end
  mix=5  pn=1 com='guide tube'              end
  mix=11 pn=1 com='fuel - inside edge zone' end
  mix=21 pn=1 com='fuel - edge zone'        end
  mix=31 pn=1 com='fuel - corner zone'      end
end materials
'
read geom
unit 1 
com='fuel rod - inner zone'
  cylinder 10   0.392176
  cylinder 20   0.40005 
  cylinder 30   0.4572
  cuboid   40 4p0.62992
  media 1 1 10
  media 4 1 20 -10
  media 2 1 30 -20
  media 3 1 40 -30
  boundary 40
unit 2 
com='fuel rod - inside edge zone'
  cylinder 10   0.392176
  cylinder 20   0.40005 
  cylinder 30   0.4572
  cuboid   40 4p0.62992
  media 11 1 10
  media  4 1 20 -10
  media  2 1 30 -20
  media  3 1 40 -30
  boundary 40
unit 3 
com='fuel rod - edge zone'
  cylinder 10   0.392176
  cylinder 20   0.40005 
  cylinder 30   0.4572
  cuboid   40 4p0.62992
  media 21 1 10
  media  4 1 20 -10
  media  2 1 30 -20
  media  3 1 40 -30
  boundary 40
unit 4 
com='fuel rod - corner zone'
  cylinder 10   0.392176
  cylinder 20   0.40005 
  cylinder 30   0.4572
  cuboid   40 4p0.62992
  media 31 1 10
  media  4 1 20 -10
  media  2 1 30 -20
  media  3 1 40 -30
  boundary 40
unit 5
com='guide tube'
  cylinder 10   0.56134
  cylinder 20   0.60198
  cuboid   40 4p0.62992
  media 3 1 10
  media 5 1 20 -10
  media 3 1 40 -20
  boundary 40
unit 11
com='1/2 fuel rod - right - inner zone'
  cylinder 10 0.392176 chord +x=0
  cylinder 20 0.40005  chord +x=0
  cylinder 30 0.4572   chord +x=0
  cuboid   40 0.62992  0.0 2p0.62992
  media 1 1 10
  media 4 1 20 -10
  media 2 1 30 -20
  media 3 1 40 -30
  boundary 40
unit 21
com='1/2 fuel rod - top - inner zone'
  cylinder 10   0.392176 chord +y=0
  cylinder 20   0.40005  chord +y=0
  cylinder 30   0.4572   chord +y=0
  cuboid   40 2p0.62992  0.62992 0.0
  media 1 1 10
  media 4 1 20 -10
  media 2 1 30 -20
  media 3 1 40 -30
  boundary 40
unit 12
com='1/2 fuel rod - right - inside edge zone'
  cylinder 10 0.392176 chord +x=0
  cylinder 20 0.40005  chord +x=0
  cylinder 30 0.4572   chord +x=0
  cuboid   40 0.62992  0.0 2p0.62992
  media 11 1 10
  media  4 1 20 -10
  media  2 1 30 -20
  media  3 1 40 -30
  boundary 40
unit 22
com='1/2 fuel rod - top - inside edge zone'
  cylinder 10   0.392176 chord +y=0
  cylinder 20   0.40005  chord +y=0
  cylinder 30   0.4572   chord +y=0
  cuboid   40 2p0.62992  0.62992 0.0
  media 11 1 10
  media  4 1 20 -10
  media  2 1 30 -20
  media  3 1 40 -30
  boundary 40
unit 13
com='1/2 fuel rod - right - edge zone'
  cylinder 10 0.392176 chord +x=0
  cylinder 20 0.40005  chord +x=0
  cylinder 30 0.4572   chord +x=0
  cuboid   40 0.62992  0.0 2p0.62992
  media 21 1 10
  media  4 1 20 -10
  media  2 1 30 -20
  media  3 1 40 -30
  boundary 40
unit 23
com='1/2 fuel rod - top - edge zone'
  cylinder 10   0.392176 chord +y=0
  cylinder 20   0.40005  chord +y=0
  cylinder 30   0.4572   chord +y=0
  cuboid   40 2p0.62992  0.62992 0.0
  media 21 1 10
  media  4 1 20 -10
  media  2 1 30 -20
  media  3 1 40 -30
  boundary 40
unit 51
com='1/2 guide tube - right'
  cylinder 10 0.56134 chord +x=0
  cylinder 20 0.60198 chord +x=0
  cuboid   40 0.62992 0.0 2p0.62992
  media 3 1 10
  media 5 1 20 -10
  media 3 1 40 -20
  boundary 40
unit 52
com='1/2 guide tube - top'
  cylinder 10   0.56134 chord +y=0
  cylinder 20   0.60198 chord +y=0
  cuboid   40 2p0.62992 0.62992 0.0
  media 3 1 10
  media 5 1 20 -10
  media 3 1 40 -20
  boundary 40
unit 53
com='1/4 instrument tube - NE'
  cylinder 10 0.56134 chord +x=0 chord +y=0
  cylinder 20 0.60198 chord +x=0 chord +y=0
  cuboid   40 0.62992 0.0 0.62992 0.0
  media 3 1 10
  media 5 1 20 -10
  media 3 1 40 -20
  boundary 40
global unit 10
com='1/4 assembly - NE'
  cuboid 10 10.75182 0.0 10.75182 0.0 
  array 1 10 place 1 1 0 0
  media 3 1 10
  boundary 10 54 54
end geom
'
read array
  ara=1 nux=9 nuy=9 typ=cuboidal 
  fill
  53  21 21 52 21 21 52 22 23
  11  1  1  1  1  1  1  2  3
  11  1  1  1  1  1  1  2  3
  51  1  1  5  1  1  5  2  3
  11  1  1  1  1  1  1  2  3
  11  1  1  1  1  5  1  2  3
  51  1  1  5  1  1  1  2  3
  12  2  2  2  2  2  2  2  4
  13  3  3  3  3  3  3  4  4  end fill
end array
'
read bnds 
  all=mirror
end bnds
'
end model
end
'
