=t-depl parm=(centrm, addnux=4)
VVER-440, 2D full assembly model with flat enrichment profile
v7-252
' ----------------------------------------------------------------
' template to generate ORIGEN-S libraries
'  parameters are: u235wt%       - wt% U235
'                  u234wt%       - wt% U234
'                  u236wt%       - wt% U236
'                  u238wt%       - wt% U238
'                  ddd           - coolant density (g/cc)
'                  namelibrary   - name of generated ORIGEN library
'                  specpow       - average specific power
'                  daystoburn    - depletion interval in days
'  options are:    name          - vver440_
'                  enrichment    - 1.6, 2.4, 3.6
'                  cool. density - 0.73
'                  spec. power   - 32.0
'                  burnups       - 0, 2, 4, 6, 9, 12, 15, 18, 21,
'                                  24, 27, 30, 33, 36, 39, 42, 45,
'                                  48, 51, 54, 57, 60, 63, 66, 69,
'                                  72, 75, 78, 81, 84
' ----------------------------------------------------------------
' Documentation and Notes (empty fields are auto-populated):
'  [Change Log] 
'    Rev 0: Generated by J. Doe | 
'    Rev 1: Generated by B. R. Betzler, June 2014
'    Rev 2: Updated and generated by B. D. Hiscox, August 2021
'  [Author(s)] B. R. Betzler
'  [SCALE Version] SCALE 6.3
'  [Reactor Type] VVER-440
'  [Model Info] 2D t-depl full assembly model with flat enrichment profile (see Figure \ref{fi:vver440}), v7-252 cross-section library
'  [Sources] 
'    1. SCALE: A Comprehensive Modeling and Simulation Suite for Nuclear Safety Analysis and Design, ORNL/TM-2005/39, Version 6.1, Oak Ridge National Laboratory, Oak Ridge, Tennessee, June 2011. |
'    2. B. D. Murphy, ``ORIGEN-ARP Cross-Section Libraries for Magnox, Advanced Gas-Cooled, and VVER Reactor Designs'', ORNL/TM-2003/263, UT-Battelle, LLC, Oak Ridge National Laboratory, February 2004.
'  [Data Range]
'  [Libraries]
'  [Power]
'  [Other Info] 
'    Flow tube information, boron concentration, and clad material from Table D1.A.6 of Reference 1 (flow tube thickness increased slightly). 
'    All other dimensions, materials, etc. from Reference 2.
'  figure{vver440.pdf: VVER-440 fuel assembly with flat enrichment profile.}
' ----------------------------------------------------------------
read comp
'
' fuel 
  uo2   1 den=10.1 1 980 92234 u234wt% 
                         92235 u235wt% 
                         92236 u236wt% 
                         92238 u238wt% end 
'
' clad - (Zr + 1%Nb + 0.05%Hf)
  wtptClad 2 6.45 3 40000 98.95 
                    41093  1.0  
                    72000  0.05 1 630 end
'
' coolant - water with 600 ppm soluble boron
  h2o   3 den=ddd 1.000000 575 end 
  boron 3 den=ddd 0.000600 575 end 
'
' gap - assumed quantities 
  he-4  4 den=0.001471 1 630 end
  he-4 14 den=0.001471 1 630 end
'
' guide tube - (Zr + 1%Nb + 0.05%Hf)
  wtptGTube 5 6.45 3 40000 98.95 
                     41093  1.0  
                     72000  0.05 1 575 end
'
' flow tube - (Zr + 2.5%Nb + 0.05%Hf)
  wtptFTube 6 6.45 3 40000 97.45
                     41093  2.5 
                     72000  0.05 1 575 end
'
end comp
'
' ----------------------------------------------------------------
read celldata
  latticecell atriangpitch imodd=0.150   4 
                           fueld=0.7566  1 
                           gapd= 0.776  14 
                           cladd=0.91    2
                           pitch=1.22    3 end
Â  centrmdata alump=0 pmc_dilute=1e30 pmc_omit=0 end centrmdata 
end celldata
'
' ----------------------------------------------------------------
read depletion
  1
end depletion
'
' ----------------------------------------------------------------
read burndata
  power=specpow burn=daystoburn down=0 end
end burndata
'
' ----------------------------------------------------------------
read model
  VVER-440, flat enrichment profile
'
read parm
  cmfd=2
  drawit=yes
  echo=yes 
  epsinner=-1e-4 
  fillmix=3
  run=yes
  xycmfd=4
  sn=8
  cell_tol=1e-8
end parm
'
read materials
  mix=1 pn=1 com='fuel'       end
  mix=2 pn=1 com='clad'       end
  mix=3 pn=2 com='coolant'    end
  mix=4 pn=0 com='gap'        end
  mix=5 pn=1 com='guide tube' end
  mix=6 pn=1 com='flow tube'  end
end materials
'
read geom
unit 1
com='fuel rod'
  cylinder 10 0.075
  cylinder 20 0.3783
  cylinder 30 0.388
  cylinder 40 0.455
  hexprism 50 0.61
  media 4 1 10
  media 1 1 20 -10
  media 4 1 30 -20
  media 2 1 40 -30
  media 3 1 50 -40
  boundary 50 6 6 
unit 5
com='central tube'
  cylinder 10 0.44
  cylinder 20 0.515
  hexprism 30 0.61
  media 3 1 10
  media 2 1 20 -10
  media 3 1 30 -20
  boundary 30 6 6
global unit 10
com='assembly'
  rhexprism 10 7.05
  rhexprism 20 7.20
  rhexprism 30 7.35
  array 1 10 place 7 7 0.0 0.0
  media 3 1 10
  media 6 1 20 -10
  media 3 1 30 -20
  boundary 30 12 12
end geom
'
read array
  ara=1 typ=shexagonal nux=13 nuy=13
  fill
  0 0 0 1 1 1 1 1 1 1 0 0 0
   0 0 1 1 1 1 1 1 1 1 0 0 0
  0 0 1 1 1 1 1 1 1 1 1 0 0
   0 1 1 1 1 1 1 1 1 1 1 0 0
  0 1 1 1 1 1 1 1 1 1 1 1 0
   1 1 1 1 1 1 1 1 1 1 1 1 0
  1 1 1 1 1 1 5 1 1 1 1 1 1
   1 1 1 1 1 1 1 1 1 1 1 1 0
  0 1 1 1 1 1 1 1 1 1 1 1 0
   0 1 1 1 1 1 1 1 1 1 1 0 0
  0 0 1 1 1 1 1 1 1 1 1 0 0
   0 0 1 1 1 1 1 1 1 1 0 0 0
  0 0 0 1 1 1 1 1 1 1 0 0 0  end fill
end array
'
read bnds 
  all=white  
end bnds
'
end model
end
'
