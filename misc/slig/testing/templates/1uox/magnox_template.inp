=t-depl parm=(centrm, addnux=4)
Calder Hall Magnox, 2D full assembly model
v7-252
' ----------------------------------------------------------------
'  template to generate libraries for ORIGEN-S 
'  parameters are: u235wt%       - wt% U235
'                  u234wt%       - wt% U234
'                  u236wt%       - wt% U236
'                  u238wt%       - wt% U238
'                  ddd           - moderator density (g/cc)
'                  namelibrary   - name of generated ORIGEN library
'                  specpow       - average specific power
'                  daystoburn    - depletion interval in days
'  options are:    name          - magnox_
'                  enrichment    - 0.7, 0.8, 0.9, 1.0
'                  mod. density  - 1.628
'                  spec. power   - 1.875
'                  burnups       - 0, 0.45, 0.9, 1.35, 1.8, 2.25,
'                                  2.7, 3.15, 3.6, 4.05, 4.5, 4.95,
'                                  5.4, 5.85, 6.3, 6.75, 7.2, 7.65,
'                                  8.1, 8.55, 9.0, 9.45, 9.9,
'                                  10.35, 10.8, 11.25, 11.7, 12.15,
'                                  12.6, 13.05, 13.5, 13.95
' ----------------------------------------------------------------
' Documentation and Notes (empty fields are auto-populated):
'  [Change Log] Rev 0: Generated by B. J. Ade, August 2014
'    Rev 1: Updated and generated by B. D. Hiscox, August 2021
'  [Author(s)] B. J. Ade
'  [SCALE Version] SCALE 6.3
'  [Reactor Type] Calder Hall Magnox
'  [Model Info] 2D t-depl full assembly model (see Figure \ref{fi:magnox}), v7-252 cross-section library
'  [Sources] 
'     1. S.E. Jensen and E. Nonbol, ``Description of the Magnox Type of Gas Cooled Reactor (MAGNOX)'', ISBN 87-7893-050-2, Riso National Laboratory, Denmark, 1999. | 
'     2. B.D. Murphy, ``ORIGEN-ARP Cross-Section Libraries for Magnox, Advanced Gas-Cooled, and VVER Reactor Designs'', ORNL/TM-2003/263, Oak Ridge National Laboratory, TN, February, 2004. 
'  [Data Range]
'  [Libraries]
'  [Power]
'  figure{magnox.pdf: Magnox reactor fuel assembly.}
' ----------------------------------------------------------------
read comp
'
' fuel
  uranium     1 den=17.98          1.0 800 92234 u234wt%
                                           92235 u235wt%
                                           92236 u236wt%
                                           92238 u238wt% end
'
' clad
  wtptmagnox  2 1.65 3 12000 98.95
                       13000  1.00
                        4000  0.05 1.0 650 end
'
' moderator
  graphite    3 den=ddd            1.0 650 end 
'
' coolant
  atomco2     4 1.507E-02 2 6000 1
                            8016 2 1.0 650 end
'
end comp
'
' ----------------------------------------------------------------
read celldata
  latticecell squarepitch fuelr= 1.461 1
                          gapr=  2.040 2
                          cladr= 4.849 4
                          pitch=20.320 3 end
Â  centrmdata alump=0 pmc_dilute=1e30 pmc_omit=0 end centrmdata
end celldata
'
' ----------------------------------------------------------------
read depletion
  1
end depletion
'
' ----------------------------------------------------------------
read burndata
  power=specpow burn=daystoburn down=0 end
end burndata
'
' ----------------------------------------------------------------
read model
  Calder Hall Magnox
'
read parm
  cmfd=1
  drawit=yes
  echo=yes
  timed=yes
  converg=mix
  xycmfd=8
  sn=8
  cell_tol=1e-8
end parm
'
read materials
  mix=1 pn=1 com='fuel - U metal '      end
  mix=2 pn=1 com='clad - Mg'            end
  mix=4 pn=1 com='coolant - CO2'        end
  mix=3 pn=2 com='moderator - graphite' end
end materials
'
read geom
global unit 1
com='assembly'
  cylinder 10    1.461 sides=18
  cylinder 20    2.040 sides=18
  cylinder 30    4.849 sides=18
  cuboid   40 4p10.16
  media 1 1  10
  media 2 1 -10 20
  media 4 1 -20 30
  media 3 1 -30 40
  boundary 40 40 40
end geom
'
read bnds
  all=mirror
end bnds
'
end model
end
'
